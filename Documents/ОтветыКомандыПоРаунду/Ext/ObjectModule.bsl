#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Ответы.Загрузить(ДанныеЗаполнения.Ответы);
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		Статус = Перечисления.СтатусыОтветов.Принято;
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;	
	КонецЕсли;
	
	РеквизитыВопросов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Ответы.ВыгрузитьКолонку("Вопрос"),
	"ЭталонныйОтвет, КоличествоБаллов");
	
	Для Каждого СтрокаОтветов Из Ответы Цикл
		
		//Система оценивает верность ответа
		Если ЭтоНовый() 
			И ОтветПринят(СтрокаОтветов.Вопрос, РеквизитыВопросов[СтрокаОтветов.Вопрос].ЭталонныйОтвет, СтрокаОтветов.Ответ) Тогда
				СтрокаОтветов.Принят = Истина;	
		КонецЕсли;
		
		Если СтрокаОтветов.Принят Тогда
			СтрокаОтветов.КоличествоБаллов = РеквизитыВопросов[СтрокаОтветов.Вопрос].КоличествоБаллов;	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ОтветПринят(Вопрос, ЭталонныйОтвет, Ответ)
	Возврат ОценкаПравильногоОтвета.ОтветПринят(Вопрос, ЭталонныйОтвет, Ответ);	
КонецФункции

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//Оставил конструктор, для ускорения разработки
	Движения.ОтветыКоманд.Записывать = Истина;
	Для Каждого ТекСтрокаОтветы Из Ответы Цикл
		Движение = Движения.ОтветыКоманд.Добавить();
		Движение.Квиз = Квиз;
		Движение.Команда = Команда;
		Движение.Раунд = Раунд;
		Движение.Вопрос = ТекСтрокаОтветы.Вопрос;
		Движение.Ответ = ТекСтрокаОтветы.Ответ;
		Движение.КоличествоБаллов = ТекСтрокаОтветы.КоличествоБаллов;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли