
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	КопироватьДанныеФормы(ВладелецФормы.Объект, Объект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Раунды.Отбор, "Ссылка", Объект.Квиз);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РаундыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Если Поле.Имя = "РаундыАктивный" Тогда
		Если Элемент.ТекущиеДанные.Активный Тогда
			СнятьАктивностьРаунда(Объект.Квиз, Элемент.ТекущиеДанные.Раунд);	
		Иначе
			УстановитьАктивностьРаунда(Объект.Квиз, Элемент.ТекущиеДанные.Раунд);	
		КонецЕсли;
		
		Элементы.Раунды.Обновить();

	Иначе
		Объект.Раунд = Элемент.ТекущиеДанные.Раунд;
		ОткрытьВыбранныйРаунд();
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОтветы;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВернутьсяНаСтраницуВыбораРаундов(Команда)
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРаунды;
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьОтветКоманды(Команда)
	
	Если Элементы.Ответы.ТекущиеДанные.ОтветКомандыПринят Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Ответы уже приняты'"));
		Возврат;
	КонецЕсли;
	
	ПринятьОтветКомандыНаСервере(Элементы.Ответы.ТекущиеДанные.Команда);
	ОткрытьВыбранныйРаунд();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьАктивностьРаунда(Знач Квиз, Знач Раунд)
		
	РегистрыСведений.АктивныеРаунды.УстановитьАктивностьРаунда(Квиз, Раунд);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СнятьАктивностьРаунда(Знач Квиз, Знач Раунд)
		
	РегистрыСведений.АктивныеРаунды.СнятьАктивностьРаунда(Квиз, Раунд);
	
КонецПроцедуры

&НаСервере
Процедура ОткрытьВыбранныйРаунд()
	
	ВопросыИОтветыРаунда = ИграВКвиз.ВопросыИОтветыРаунда(Объект.Квиз, Объект.Раунд);
	Ответы.Загрузить(ВопросыИОтветыРаунда);
	
КонецПроцедуры

&НаСервере
Процедура ПринятьОтветКомандыНаСервере(Знач ВыбраннаяКоманда)
	
	ПараметрыОтбора = Новый Структура("Команда", ВыбраннаяКоманда);
	Строки = Ответы.НайтиСтроки(ПараметрыОтбора);
	
	ИграВКвиз.ПроверитьОтветКоманды(Объект.Квиз, Объект.Раунд, ВыбраннаяКоманда, Строки);
	
КонецПроцедуры

&НаСервере
Процедура ПечатьИтоговПоРаундуНаСервере(ТабличныйДокумент)
	
	//Взято из конструктура и немного изменено.
	Документы.ОтветыКомандыПоРаунду.Печать(ТабличныйДокумент, Объект.Квиз, Объект.Раунд);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьИтоговПоРаунду(Команда)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ПечатьИтоговПоРаундуНаСервере(ТабличныйДокумент);

	ТабличныйДокумент.ОтображатьСетку = Ложь;
	ТабличныйДокумент.Защита = Ложь;
	ТабличныйДокумент.ТолькоПросмотр = Ложь;
	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.Показать();
	
КонецПроцедуры

#КонецОбласти
