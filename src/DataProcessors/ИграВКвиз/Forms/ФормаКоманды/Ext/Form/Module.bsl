
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	КопироватьДанныеФормы(ВладелецФормы.Объект, Объект);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Раунды.Отбор, "Ссылка", Объект.Квиз);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РаундыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не Элемент.ТекущиеДанные.Активный Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выбирать можно только активный раунд'"));	
		Возврат;	
	КонецЕсли;
	
	Объект.Раунд = Элемент.ТекущиеДанные.Раунд;
	ЗаполнитьВопросыПоРаунду(Объект.Раунд);
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВводаОтвета;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветыНаВопросыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТипОтвета = Строка(ОтветыНаВопросы.НайтиПоИдентификатору(ВыбраннаяСтрока).ТипОтвета);
	Поле.ОграничениеТипа = Новый ОписаниеТипов(ТипОтвета);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветыНаВопросыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветыНаВопросыПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	Элементы.Раунды.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ЗафиксироватьОтветы(Команда)

	ЗафиксироватьОтветыНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыбораРаунда;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗафиксироватьОтветыНаСервере()
	
	ОтветыКоманды = Новый Структура();
	ОтветыКоманды.Вставить("Квиз", Объект.Квиз);
	ОтветыКоманды.Вставить("Команда", Объект.Команда);
	ОтветыКоманды.Вставить("Раунд", ОтветыНаВопросы[0].Раунд);
	ОтветыКоманды.Вставить("Ответы", ОтветыНаВопросы.Выгрузить());
	ОтветыКоманды.Вставить("Дата", ТекущаяДатаСеанса());
	
	Попытка
		Документы.ОтветыКомандыПоРаунду.ПринятьОтветКоманды(ОтветыКоманды);
		
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ТекстОшибкиДляЗаписиВЖурнал = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ПояснениеКОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке);
		ОбщегоНазначения.СообщитьПользователю(ПояснениеКОшибке);
		
		УровеньОшибки = УровеньЖурналаРегистрации.Предупреждение;
		ЗаписьЖурналаРегистрации("Приемка ответов", УровеньОшибки, , ,ТекстОшибкиДляЗаписиВЖурнал);
		ВызватьИсключение;
	КонецПопытки;

	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыбораРаунда;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВопросыПоРаунду(Знач Раунд)
	
	Вопросы = Справочники.Квизы.ВопросыПоРаунду(Объект.Квиз, Раунд);
	
	ОтветыНаВопросы.Загрузить(Вопросы);
	
КонецПроцедуры

#КонецОбласти
