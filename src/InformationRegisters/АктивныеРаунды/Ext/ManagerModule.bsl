#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура УстановитьАктивностьРаунда(Квиз, Раунд) Экспорт
	
	Запись = РегистрыСведений.АктивныеРаунды.СоздатьМенеджерЗаписи();
	Запись.Квиз = Квиз;
	Запись.Раунд = Раунд;
	Запись.Записать();
	
КонецПроцедуры

Процедура СнятьАктивностьРаунда(Квиз, Раунд) Экспорт
	
	Набор = РегистрыСведений.АктивныеРаунды.СоздатьНаборЗаписей();
	Набор.Отбор.Квиз.Установить(Квиз);
	Набор.Записать();
	
КонецПроцедуры

Функция АктивныйРаундКвиза(Квиз) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктивныеРаунды.Раунд КАК Раунд
		|ИЗ
		|	РегистрСведений.АктивныеРаунды КАК АктивныеРаунды
		|ГДЕ
		|	АктивныеРаунды.Квиз = &Квиз";
	
	Запрос.УстановитьПараметр("Квиз", Квиз);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Раунд;
	Иначе
		Возврат Справочники.Раунды.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли
