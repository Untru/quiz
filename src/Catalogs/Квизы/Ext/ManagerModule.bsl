#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВопросыПоРаунду(Квиз, Раунд) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КвизыВопросы.Вопрос КАК Вопрос,
	|	Вопросы.ТипОтвета КАК ТипОтвета,
	|	КвизыВопросы.КлючСвязи КАК НомерВопроса,
	|	КвизыРаунды.Раунд КАК Раунд,
	|	КвизыВопросы.Ссылка КАК Ссылка,
	|	КвизыРаунды.Ссылка КАК Ссылка1
	|ИЗ
	|	Справочник.Квизы.Раунды КАК КвизыРаунды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Квизы.Вопросы КАК КвизыВопросы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Вопросы КАК Вопросы
	|			ПО КвизыВопросы.Вопрос = Вопросы.Ссылка
	|		ПО (КвизыВопросы.КлючСвязи = КвизыРаунды.НомерСтроки)
	|ГДЕ
	|	КвизыРаунды.Раунд = &Раунд
	|	И КвизыРаунды.Ссылка = &Квиз
	|	И КвизыВопросы.Ссылка = &Квиз";
	
	Запрос.УстановитьПараметр("Раунд", Раунд);
	Запрос.УстановитьПараметр("Квиз", Квиз);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция КомандаКвизПоЛогину(Квиз, Логин) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КвизыКоманды.Команда КАК Команда
		|ИЗ
		|	Справочник.Квизы.Команды КАК КвизыКоманды
		|ГДЕ
		|	КвизыКоманды.Логин = &Логин
		|	И КвизыКоманды.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Логин", Логин);
	Запрос.УстановитьПараметр("Ссылка", Квиз);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Команда = Справочники.Команды.ПустаяСсылка();
	Если Выборка.Следующий() Тогда
		Команда = Выборка.Команда;
	КонецЕсли;
	
	Возврат Команда;
	
КонецФункции

Функция АвторизацияПройдена(Квиз, Логин, Пароль) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ПарольИзХранилища = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Квиз, Логин);
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ПарольИзХранилища = Пароль;
	
КонецФункции

Функция КомандаУжеУчаствуетВКвиз(Квиз, Команда) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КвизыКоманды.Ссылка КАК Квиз
		|ИЗ
		|	Справочник.Квизы.Команды КАК КвизыКоманды
		|ГДЕ
		|	КвизыКоманды.Ссылка = &Квиз
		|	И КвизыКоманды.Команда = &Команда";
	
	Запрос.УстановитьПараметр("Квиз", Квиз);
	Запрос.УстановитьПараметр("Команда", Команда);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли



