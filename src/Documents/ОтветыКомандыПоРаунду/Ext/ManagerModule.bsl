#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс
//Тут описать входящую структуру
Процедура ПринятьОтветКоманды(ДанныеОтвета) Экспорт
	
	Если ДанныеОтвета.Раунд <> РегистрыСведений.АктивныеРаунды.АктивныйРаундКвиза(ДанныеОтвета.Квиз) Тогда
		ВызватьИсключение(
				НСтр("ru = 'По текущему раунду больше ответы не принимаются'"),
				,
				"ОшибкаОтправкиОтветовРаундНеАктивный");
	КонецЕсли;
	
	Ответ = ОтветКоманды(ДанныеОтвета.Квиз, ДанныеОтвета.Раунд, ДанныеОтвета.Команда);
	Если ЗначениеЗаполнено(Ответ) Тогда
		ВызватьИсключение(
				НСтр("ru = 'По текущему раунду уже есть ответ'"),
				,
				"ОшибкаОтправкиОтветовУжеЕстьОтвет");
	КонецЕсли;
	
	НовыйОбъект = СоздатьДокумент();
	НовыйОбъект.Заполнить(ДанныеОтвета);
	НовыйОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

Функция ОтветКоманды(Квиз, Раунд, Команда) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтветыКомандыПоРаунду.Ссылка КАК ОтветыКоманды
		|ИЗ
		|	Документ.ОтветыКомандыПоРаунду КАК ОтветыКомандыПоРаунду
		|ГДЕ
		|	ОтветыКомандыПоРаунду.Квиз = &Квиз
		|	И ОтветыКомандыПоРаунду.Раунд = &Раунд
		|	И ОтветыКомандыПоРаунду.Команда = &Команда";
	
	Запрос.УстановитьПараметр("Квиз", Квиз);
	Запрос.УстановитьПараметр("Команда", Команда);
	Запрос.УстановитьПараметр("Раунд", Раунд);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();

	Возврат Выборка.ОтветыКоманды;
	
КонецФункции

Процедура Печать(ТабличныйДокумент, Квиз, Раунд) Экспорт
	
	//Конструктор, без рефакторинга, только вынес запрос
	Макет = Документы.ОтветыКомандыПоРаунду.ПолучитьМакет("Печать");
	
	Выборка = ВыборкаРезультатыРаунда(Квиз, Раунд);
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьОтветыШапка = Макет.ПолучитьОбласть("ОтветыШапка");
	ОбластьОтветы = Макет.ПолучитьОбласть("Ответы");
	ТабличныйДокумент.Очистить();

	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		ТабличныйДокумент.Вывести(ОбластьЗаголовок);

		Шапка.Параметры.Заполнить(Выборка);
		ТабличныйДокумент.Вывести(Шапка, Выборка.Уровень());

		ТабличныйДокумент.Вывести(ОбластьОтветыШапка);
		ВыборкаОтветы = Выборка.Ответы.Выбрать();
		Пока ВыборкаОтветы.Следующий() Цикл
			ОбластьОтветы.Параметры.Заполнить(ВыборкаОтветы);
			ТабличныйДокумент.Вывести(ОбластьОтветы, ВыборкаОтветы.Уровень());
		КонецЦикла;

		ВставлятьРазделительСтраниц = Истина;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВыборкаРезультатыРаунда(Квиз, Раунд)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтветыКомандыПоРаунду.Квиз,
	|	ОтветыКомандыПоРаунду.Команда,
	|	ОтветыКомандыПоРаунду.Раунд,
	|	ОтветыКомандыПоРаунду.Ответы.(
	|		Вопрос,
	|		Ответ,
	|		Принят,
	|		КоличествоБаллов
	|	)
	|ИЗ
	|	Документ.ОтветыКомандыПоРаунду КАК ОтветыКомандыПоРаунду
	|ГДЕ
	|	ОтветыКомандыПоРаунду.Квиз = &Квиз
	|	И ОтветыКомандыПоРаунду.Раунд = &Раунд";
	Запрос.Параметры.Вставить("Квиз", Квиз);
	Запрос.Параметры.Вставить("Раунд", Раунд);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

#КонецОбласти

#КонецЕсли
